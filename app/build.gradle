apply plugin: 'com.android.application'

def apiKeyProps = new Properties()
apiKeyProps.load(new FileInputStream(rootProject.file("apikey.properties")))

android {
    compileSdkVersion 25
    buildToolsVersion "24.0.3"
    defaultConfig {
        applicationId "com.weizhi.sg_psi"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resValue "string", "sg_gov_data_key", apiKeyProps.sgDataGovApiKey
    }
    signingConfigs {
        release
    }
    buildTypes {
        debug {
            versionNameSuffix "-debug"
            manifestPlaceholders = [ googleMapApiKey:apiKeyProps.googleMapApiKeyDebug]
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [ googleMapApiKey:apiKeyProps.googleMapApiKeyRelease]
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

def keyStoreProps = new Properties()
keyStoreProps.load(new FileInputStream(rootProject.file("release.properties")))
android.signingConfigs.release.storeFile rootProject.file(keyStoreProps.keyStore)
android.signingConfigs.release.storePassword keyStoreProps.keyStorePassword
android.signingConfigs.release.keyAlias keyStoreProps.keyAlias
android.signingConfigs.release.keyPassword keyStoreProps.keyAliasPassword

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:design:25.1.0'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-jackson:2.1.0'
    compile 'com.jakewharton.timber:timber:4.1.2'
    compile 'com.birbit:android-priority-jobqueue:2.0.1'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.google.android.gms:play-services-maps:10.0.1'
    compile 'com.google.android.gms:play-services-gcm:10.0.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.2.21'
}
